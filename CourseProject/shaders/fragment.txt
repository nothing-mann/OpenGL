#version 330 core

struct PointLight{
    vec3 position;
    vec3 color;
    float strength;
};

in vec2 fragmentTexCoord;
in vec3 fragmentPosition;
in vec3 fragmentNormal;

uniform sampler2D imageTexture;
uniform PointLight Lights[4];
uniform vec3 cameraPosition;

out vec4 color;

vec3 calculatePointLight(PointLight light, vec3 fragmentPosition, vec3 fragmentNormal);

void main()
{
    vec3 temp = vec3(0.0);
    //ambient
    temp += 0.2 *  texture(imageTexture, fragmentTexCoord).rgb;
    for (int i = 0; i < 4; i++)
    {
        temp += calculatePointLight(Lights[i], fragmentPosition, fragmentNormal);
    }
    color = vec4(temp, 1.0);
}

vec3 calculatePointLight(PointLight light, vec3 fragmentPosition, vec3 fragmentNormal)
{

    vec3 result = vec3(0.0);
    vec3 baseTexture = texture(imageTexture, fragmentTexCoord).rgb;

    //geometric data
    vec3 fragLight = light.position - fragmentPosition;
    float distance = length(fragLight);
    fragLight = normalize(fragLight);
    vec3 fragCamera = normalize(cameraPosition) - fragmentPosition;
    vec3 halfVec = normalize(fragLight + fragCamera);


    //diffuse
    result += light.color * light.strength * max(0.0, dot(fragmentNormal, fragLight)) / (distance * distance) * baseTexture;
    //specular
    result += vec3(1,1,1) * light.strength * pow(max(0.0, dot(fragmentNormal, halfVec)), 32) / (distance * distance);

    return result;
}